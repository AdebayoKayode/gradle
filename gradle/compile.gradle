ext.java7Home=System.getProperty('java7.home')

if (java7Home) {
    def javaExecutablesPath = new File(java7Home, 'bin')
    def isWindows = org.gradle.internal.os.OperatingSystem.current().windows
    def javaExecutables = [:].withDefault { execName ->
        def executable = new File(javaExecutablesPath, execName + (isWindows ? ".exe" : ""))
        assert executable.exists() : "There is no ${execName} executable in ${javaExecutablesPath}"
        executable
    }
    tasks.withType(JavaCompile) {
        options.with {
            fork = true
            forkOptions.executable = javaExecutables.javac
        }
    }
    tasks.withType(Javadoc) {
        executable = javaExecutables.javadoc
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'utf-8'
    options.compilerArgs = ['-Xlint:-options', '-Xlint:-path']
}
tasks.withType(GroovyCompile) {
    options.encoding = 'utf-8'
    options.compilerArgs = ['-Xlint:-options', '-Xlint:-path']
    groovyOptions.encoding = 'utf-8'
    if (!JavaVersion.current().java8Compatible) {
        options.forkOptions.jvmArgs = ['-XX:MaxPermSize=512m']
    }
}
